# Load dependencies.
pmodload 'helper'

function prompt_russtone_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_russtone_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_russtone_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{magenta}▶︎%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{cyan}◼︎%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{magenta}%s%f'
  zstyle ':prezto:module:git:info:branch' format '%F{magenta}(%b)%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:commit' format '%F{blue}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{magenta}(%p)%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' $(coalesce "%b" "%p" "%c")%s%C%D'

  # Define prompt.
  PROMPT='
%F{blue}%~%f${git_info:+${(e)git_info[prompt]}}
${editor_info[keymap]} '

}

prompt_russtone_setup "$@"
